import array;
import box;
import docs;
import iter;

let doc = docs.module("list", "A linked list implementation.");

struct Node {
  car, cdr
}

export function new() {
  return box.new(null);
}

doc:add("function", "new()", "Create a new, empty linked list.");

export function iter(self) {
  let current = box.get(self);
  return function list_iterator() {
    if (current == null) {
      return null;
    }
    let node = current;
    current = node:cdr;
    return node:car;
  };
}

doc:add("function", "iter(list)", "Create an iterator over `list`.");

export function length(self) {
  let current = box.get(self);
  let len = 0;

  while (current) {
    len = len + 1;
    current = current:cdr;
  }

  return len;
}

doc:add("function", "length(list)", "Get the length of the list.

Note that this is O(n) (i.e. requires traversing the entire list).");

export function prepend(self, value) {
  let current = box.get(self);
  box.set(self, Node{car=value, cdr=current});
}

doc:add("function", "prepend(list, value)", "Add a value to the front of the list.

This function is O(1) (i.e. takes the same time regardless of the length of the list).");

export function append(self, value) {
  let current = box.get(self);
  let prev = null;
  if (current == null) {
    box.set(self, Node{car=value, cdr=null});
    return;
  }

  while (current) {
    prev = current;
    current = current:cdr;
  }

  prev:cdr = [value, null];
}

doc:add("function", "append(list, value)", "Add a value to the end of the list.

Like `length`, this function is O(n).");

export function foreach(self, func) {
  let current = box.get(self);

  for (let i = 0; current; i = i + 1) {
    func(current:car, i, self);
    current = current:cdr;
  }
}

doc:add("function", "foreach(list, func)", "Call `func` for every element of `list`.");

export function find(self, func) {
  let current = box.get(self);

  for (let i = 0; current; i = i + 1) {
    if (func(current:car, i, self)) {
      return current:car;
    }
  }

  return null;
}

doc:add(
  "function",
  "find(list, func)",
  "Return the first element of `list` that satisfies the predicate `func`.

If none is found, this function returns `null`.",
);

export function reverse(self) {
  let new_list = new();

  foreach(self, function(val) {
    prepend(new_list, val);
  });

  return new_list;
}

doc:add("function", "reverse(list)", "Make a new linked list that is the reverse of `list`.");

export let collector = iter.collector {
  init = new,
  reduce = function (list, value) {
    prepend(list, value);
    return list;
  },
  finalize = reverse,
};

doc:add("object", "collector", "A collector to convert an iterator into a list.");

export function map(self, func) {
  let new_list = new();

  foreach(self, function(val, i) {
    prepend(new_list, func(val, i, self));
  });

  return reverse(new_list);
}

doc:add(
  "function",
  "map(list, func)",
  "Create a new linked list where every element is calculated by calling `func` on the old element.",
);

export function to_array(self) {
  let arr = array.new(length(self));

  foreach(self, function(val, i) {
    arr[i] = val;
  });

  return arr;
}

doc:add("function", "to_array(list)", "Create a new array with the elements of `list`.");
