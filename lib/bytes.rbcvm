import _bytes;
import arraylist;
import docs;
import iter;

let doc = docs::module("bytes", "A compact byte array type.");

export struct Error { message }

export let new = _bytes::new;
export let copy = _bytes::copy;
export let get = _bytes::get;
export let set = _bytes::set;
export let length = _bytes::length;

doc.add("function", "new(size)", "Create a new byte array of size `size`.");
doc.add("function", "copy(from, to, n)", "Copy `n` bytes from `from` to `to`.");
doc.add("function", "get(bytes, i)", "Get the `i`th element from `bytes`.");
doc.add("function", "set(bytes, i, val)", "Set the `i`th value in `bytes` to `val`.");
doc.add("function", "length(bytes)", "Get the length of `bytes`.");

export function slice(bytes, start=0, end=-1) {
    if (end == -1) {
        end = length(bytes);
    }

    if (end < start) {
        throw Error { message = "slice end must be greater or equal to start" };
    }

    let len = end - start;
    let new_bytes = new(len);
    for (let i = 0; i < len; i = i + 1) {
        set(new_bytes, i, get(bytes, i + start));
    }
    return new_bytes;
}

export function concat(a, b) {
    let a_len = length(a),
        b_len = length(b);
    let result = new(a_len + b_len);
    let i = 0;
    for (; i < a_len; i = i + 1) {
        set(result, i, get(a, i));
    }
    for (; i < b_len; i = i + 1) {
        set(result, i, get(b, i - a_len));
    }
    return result;
}

export let collector = iter::collector {
    init = arraylist::new,
    reduce = arraylist::push,
    finalize = function collect_bytes(list) {
        let len = arraylist::length(list);
        let bytes = new(len);
        for (let i = 0; i < len; i = i + 1) {
            set(bytes, i, arraylist::get(list, i));
        }
        return bytes;
    },
};

doc.add("object", "collector", "A collector for converting an iterator into byte array.");

export function iter(bytes) {
    let i = 0;
    let len = length(bytes);
    return function bytes_iter() {
        if (i >= len) {
            return iter::STOP;
        }
        let val = get(bytes, i);
        i = i + 1;
        return val;
    };
}

doc.add("function", "iter(bytes)", "Create an iterator over `bytes`.");