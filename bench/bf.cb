import array;
import arraylist;
import fs;
import iter;
import string;

struct Tape {
    pos,
    data,

    function get() {
        return arraylist::get(this.data, this.pos);
    }

    function inc(n) {
        arraylist::set(this.data, this.pos,
            arraylist::get(this.data, this.pos) + n);
    }

    function move(n) {
        this.pos += n;
        if (this.pos >= arraylist::length(this.data)) {
            arraylist::push(this.data, 0);
        }
    }
}

function tape_new() {
    return Tape {
        pos = 0,
        data = arraylist::from_array(1, [0]),
    };
}

struct Op { type, value }

function parse(it) {
    let ops = arraylist::new();
    let c;

    while ((c = iter::next(it)) != iter::STOP) {
        if (c == '+') {
            arraylist::push(ops, Op{type="inc", value=1});
        } else if (c == '-') {
            arraylist::push(ops, Op{type="inc", value=-1});
        } else if (c == '>') {
            arraylist::push(ops, Op{type="move", value=1});
        } else if (c == '<') {
            arraylist::push(ops, Op{type="move", value=-1});
        } else if (c == '.') {
            arraylist::push(ops, Op{type="print"});
        } else if (c == '[') {
            arraylist::push(ops, Op{type="loop", value=parse(it)});
        } else if (c == ']') {
            break;
        }
    }

    return arraylist::to_array(ops);
}

function run(tape, ops) {
    let len = array::length(ops);

    for (let i = 0; i < len; i += 1) {
        let op = ops[i];

        if (op.type == "inc") {
            tape.inc(op.value);
        } else if (op.type == "move") {
            tape.move(op.value);
        } else if (op.type == "print") {
            print(chr(tape.get()));
        } else if (op.type == "loop") {
            while (tape.get() != 0) {
                run(tape, op.value);
            }
        }
    }
}

let args = argv();
if (array::length(args) != 1) {
    println("Usage: ./cbcvm bf.cb <program>");
} else {
    let it = iter::iter(fs::read_file(args[0]));
    run(tape_new(), parse(it));
}